load("//rule:merge_library.bzl", "kt_merge_library", "merge_library_jar")
load("//rule:remap_jar.bzl", "remap_jar")
load("//rule/fabric:mod_json_jar.bzl", "fabric_mod_json_jar")
load("//touchcontroller:properties.bzl", "touchcontroller_authors", "touchcontroller_contributors", "touchcontroller_description", "touchcontroller_homepage", "touchcontroller_issue_tracker", "touchcontroller_license", "touchcontroller_source", "touchcontroller_version")

fabric_mod_json_jar(
    name = "fabric_mod_json",
    src = "resources/fabric.mod.json",
    resource_strip_prefix = "touchcontroller/versions/1.21.11/fabric",
    substitutions = {
        "${version}": touchcontroller_version,
        "${license}": touchcontroller_license,
        "${home_page}": touchcontroller_homepage,
        "${sources_page}": touchcontroller_source,
        "${issue_tracker}": touchcontroller_issue_tracker,
        "${description}": touchcontroller_description,
        '"mod_authors_array"': json.encode(touchcontroller_authors),
        '"mod_contributors_array"': json.encode(touchcontroller_contributors),
    },
)

remap_jar(
    name = "remapped_deps",
    classpath = ["//game/1.21.11:game_client_intermediary"],
    from_namespace = "intermediary",
    inputs = [
        "@maven_fabric_1_21_11//:net_fabricmc_fabric_api_fabric_api",
    ],
    mapping = "//game/1.21.11:game_merged_mapping",
    mixin = True,
    remap_transitive_deps = True,
    remove_jar_in_jar = True,
    to_namespace = "named",
)

kt_merge_library(
    name = "fabric_unmapped",
    srcs = glob([
        "**/*.kt",
        "**/*.java",
    ]),
    actual = True,
    merge_deps = [
        "//touchcontroller/buildinfo",
        "//touchcontroller/common/config/holder",
        "//touchcontroller/common/event/block",
        "//touchcontroller/common/event/connection",
        "//touchcontroller/common/event/key",
        "//touchcontroller/common/event/render",
        "//touchcontroller/common/event/tick",
        "//touchcontroller/common/event/window",
        "//touchcontroller/common/gal/config",
        "//touchcontroller/common/gal/key",
        "//touchcontroller/common/layout/data",
        "//touchcontroller/common/model",
        "//touchcontroller/versions/1.21.11/extension",
        "//touchcontroller/versions/1.21.11/gal",
        "//touchcontroller/versions/1.21.11/mixin",
    ],
    merge_runtime_deps = [
        "//touchcontroller/common:common_runtime_deps",
        "//touchcontroller/resources/texture:vanilla_lib",
        "//touchcontroller/versions/1.21.11/widget",
    ],
    resource_jars = [
        ":fabric_mod_json",
        "//touchcontroller/resources/texture:vanilla_pack",
        "//touchcontroller/resources/lang",
        "//touchcontroller/resources/texture:icon_jar",
    ],
    resource_strip_prefix = "touchcontroller/versions/1.21.11/fabric/resources",
    resources = glob(
        include = ["resources/**"],
        exclude = ["resources/fabric.mod.json"],
    ),
    visibility = ["//visibility:public"],
    runtime_deps = [
        "//combine/theme/blackstone:vanilla_lib_unmerged",
    ],
    deps = [
        ":remapped_deps",
        "//combine/backend/minecraft/1.21.11",
        "//combine/core/data",
        "//game/1.21.11:game_client_named",
        "@maven_fabric_1_21_11//:io_github_llamalad7_mixinextras_fabric",
        "@maven_fabric_1_21_11//:net_fabricmc_fabric_loader",
        "@maven_fabric_1_21_11//:net_fabricmc_sponge_mixin",
        "@minecraft//1.21.11:client_libraries",
    ],
)

merge_library_jar(
    name = "fabric_unmapped_merged",
    deps = [":fabric_unmapped"],
)

remap_jar(
    name = "fabric_merged",
    classpath = ["//game/1.21.11:game_client_named"],
    from_namespace = "named",
    inputs = [":fabric_unmapped_merged"],
    mapping = "//game/1.21.11:game_merged_mapping",
    mixin = True,
    to_namespace = "intermediary",
)

java_binary(
    name = "dev_client",
    srcs = [],
    data = [
        "//game/1.21.11:game_client_assets",
        "@minecraft_assets//:assets",
    ],
    env = {
        "LANG": "en_US.UTF8",
    },
    jvm_flags = [
        "-Dfabric.development=true",
        "-Ddev.launch.version=1.21.11",
        "-Ddev.launch.type=client",
        "-Ddev.launch.assetsPath=$(rlocationpath @minecraft_assets//:assets)",
        "-Ddev.launch.mainClass=net.fabricmc.loader.impl.launch.knot.KnotClient",
        "-Xmx4G",
    ],
    main_class = "top.fifthlight.fabazel.devlaunchwrapper.DevLaunchWrapper",
    runtime_deps = [
        "//game/1.21.11:game_client_named",
        "//game/1.21.11:game_client_libraries",
        # Classpath separator
        "//game/1.21.11:game_mapping_jar",
        ":fabric_unmapped",
        ":remapped_deps",
        "//combine/backend/minecraft/1.21.11/fabric:fabric_unmapped_merged",
        "//rule/dev_launch_wrapper",
        "@maven_fabric_1_21_11//:net_fabricmc_fabric_language_kotlin",
        "@maven_fabric_1_21_11//:net_fabricmc_fabric_loader",
        "@maven_fabric_1_21_11//:net_fabricmc_sponge_mixin",
    ],
)
