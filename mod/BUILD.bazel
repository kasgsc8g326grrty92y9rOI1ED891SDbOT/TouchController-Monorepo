load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_java//java:defs.bzl", "java_binary", "java_library")
load("@rules_kotlin//kotlin:jvm.bzl", "kt_jvm_library", "kt_jvm_test")
load("//:properties.bzl", "game_version", "mod_version", "fabric_api_version_modrinth", "fabric_language_kotlin_version_modrinth")
load("@//rule:upload_modrinth.bzl", "modrinth_dependency", "upload_modrinth")
load("//rule:apply_access_widener.bzl", "apply_access_widener")
load("//rule:decompile_jar.bzl", "decompile_jar")
load("//rule:extract_access_widener.bzl", "extract_access_widener")
load("//rule:fabric_mod_json_jar.bzl", "fabric_mod_json_jar")
load("//rule:generate_remap_classpath.bzl", "generate_remap_classpath")
load("//rule:merge_jar.bzl", "merge_jar")
load("//rule:merge_jij.bzl", "merge_jij")
load("//rule:remap_jar.bzl", "remap_jar")

extract_access_widener(
    name = "access_widener_dep_intermediary",
    inputs = [
        "@maven//:net_fabricmc_fabric_api_fabric_api",
        "@maven//:com_terraformersmc_modmenu",
    ],
)

remap_jar(
    name = "remapped_deps",
    classpath = [
        "//game:remapped_client_intermediary",
    ],
    from_namespace = "intermediary",
    inputs = [
        "@maven//:net_fabricmc_fabric_api_fabric_api",
        "@maven//:com_terraformersmc_modmenu",
        "@maven//:eu_pb4_placeholder_api",
    ],
    mapping = "//game:merged_mapping",
    mixin = True,
    remap_transitive_deps = True,
    remove_jar_in_jar = True,
    to_namespace = "named",
)

extract_access_widener(
    name = "access_widener_dep_named",
    inputs = [":remapped_deps"],
)

apply_access_widener(
    name = "remapped_client_access_widened_named",
    srcs = [
        "src/main/resources/armorstand.accesswidener",
        ":access_widener_dep_named",
    ],
    input = "//game:remapped_client_named",
)

decompile_jar(
    name = "decompile_client_access_widened_named",
    inputs = [":remapped_client_access_widened_named"],
)

apply_access_widener(
    name = "remapped_server_access_widened_named",
    srcs = [
        "src/main/resources/armorstand.accesswidener",
        ":access_widener_dep_named",
    ],
    input = "//game:remapped_server_named",
)

fabric_mod_json_jar(
    name = "fabric_mod_json",
    src = "src/main/resources/fabric.mod.json",
    resource_strip_prefix = "mod",
    substitutions = {
        "${version}": mod_version,
    },
)

kt_jvm_library(
    name = "mod_main_unmapped",
    srcs = glob(
        include = [
            "src/main/kotlin/**/*.kt",
            "src/main/java/**/*.java",
        ],
        allow_empty = True,
    ),
    module_name = "top_fifthlight_armorstand",
    resource_jars = [":fabric_mod_json"],
    resources = glob(
        include = ["src/main/resources/**"],
        exclude = ["src/main/resources/fabric.mod.json"],
    ),
    deps = [
        ":remapped_deps",
        ":remapped_server_access_widened_named",
        "//:kotlin_serialization",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@minecraft//:%s_server_libraries" % game_version,
    ],
)

remap_jar(
    name = "mod_main",
    classpath = ["//game:remapped_client_named"],
    from_namespace = "named",
    inputs = [":mod_main_unmapped"],
    mapping = "//game:merged_mapping",
    mixin = True,
    to_namespace = "intermediary",
)

kt_jvm_library(
    name = "mod_client_unmapped",
    srcs = glob(
        include = [
            "src/client/kotlin/**/*.kt",
            "src/client/java/**/*.java",
        ],
        allow_empty = True,
    ),
    module_name = "top_fifthlight_armorstand",
    resource_jars = ["//blazerod/model/example-models:default_models"],
    resource_strip_prefix = "mod/src/client/resources",
    resources = glob(
        include = ["src/client/resources/**"],
        allow_empty = True,
    ),
    deps = [
        ":mod_main_unmapped",
        ":remapped_client_access_widened_named",
        ":remapped_deps",
        "//:kotlin_serialization",
        "//blazerod:blazerod_without_jij",
        "@maven//:com_h2database_h2",
        "@maven//:com_illposed_osc_javaosc_core",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@minecraft//:%s_client_libraries" % game_version,
    ],
)

remap_jar(
    name = "mod_client",
    classpath = ["//game:remapped_client_named"],
    from_namespace = "named",
    inputs = [":mod_client_unmapped"],
    mapping = "//game:merged_mapping",
    mixin = True,
    to_namespace = "intermediary",
)

merge_jar(
    name = "mod_unmapped",
    deps = [
        ":mod_client_unmapped",
        ":mod_main_unmapped",
    ],
    resources = {
        "//third_party/licenses": "third_party",
        "//:license": "",
    },
)

merge_jar(
    name = "mod_without_jij",
    deps = [
        ":mod_client",
        ":mod_main",
    ],
    resources = {
        "//third_party/licenses": "third_party",
        "//:license": "",
    },
)

merge_jij(
    name = "mod",
    input = ":mod_without_jij",
    visibility = ["//visibility:public"],
    deps = {
        "@maven//:com_h2database_h2": "com_h2database_h2:2.3.232",
        "@maven//:com_illposed_osc_javaosc_core": "com_illposed_osc_javaosc_core:0.9",
        "@mocha//:mocha": "team_unnamed_mocha:3.0.1",
        "@maven//:org_javassist_javassist": "org_javassist_javassist:3.30.2-GA",
        "//blazerod:blazerod_without_jij": "blazerod:=",
        "//blazerod/model/model-base": "blazerod-model-base:=",
        "//blazerod/model/model-formats": "blazerod-model-formats:=",
        "//blazerod/model/model-gltf": "blazerod-model-formats-gltf:=",
        "//blazerod/model/model-pmd": "blazerod-model-formats-pmd:=",
        "//blazerod/model/model-pmx": "blazerod-model-formats-pmx:=",
        "//blazerod/model/model-vmd": "blazerod-model-formats-vmd:=",
        "//blazerod/model/model-bedrock": "blazerod-model-formats-bedrock:=",
        "//blazerod/render": "blazerod-render:=",
    },
)

generate_remap_classpath(
    name = "remap_classpath",
    prefix = "../../../../../../",
    deps = [
        ":remapped_deps",
        "//game:remapped_client_intermediary",
    ],
)

java_binary(
    name = "client",
    srcs = [],
    data = [
        ":remap_classpath",
        "@minecraft_assets//:assets_%s" % game_version,
        "@minecraft_assets//:assets",
        "@makeup_ultra_fast//file",
        "@yes_steve_model//:model_wine_fox",
        "@yes_steve_model//:model_default",
        "//blazerod/model/example-models:example-models",
    ],
    env = {
        "LANG": "en_US.UTF8",
    },
    jvm_flags = [
        "-Dkotlinx.coroutines.debug=on",
        "-Dfabric.development=true",
        "-Dfabric.remapClasspathFile=$(rlocationpath :remap_classpath)",
        "-Ddev.launch.version=%s" % game_version,
        "-Ddev.launch.type=client",
        "-Ddev.launch.assetsPath=$(rlocationpath @minecraft_assets//:assets)",
        "-Ddev.launch.mainClass=net.fabricmc.loader.impl.launch.knot.KnotClient",
        "-Ddev.launch.copyFiles=" +
            "$(rlocationpath @makeup_ultra_fast//file):shaderpacks/MakeUp-UltraFast-9.2.zip," +
            "$(rlocationpath //blazerod/model/example-models:example-models):models," +
            "$(rlocationpath @yes_steve_model//:model_wine_fox):models/wine_fox," +
            "$(rlocationpath @yes_steve_model//:model_default):models/default",
        "-Ddev.launch.expandRunfileProperties=fabric.remapClasspathFile",
        "-Dblazerod.debug=true",
        "-Dblazerod.debug.gui=true",
        "-Darmorstand.debug=true",
        "-Darmorstand.debug.bone=false",
        "-Darmorstand.debug.gui=false",
        "-Xmx4G",
    ],
    main_class = "top.fifthlight.fabazel.devlaunchwrapper.DevLaunchWrapper",
    runtime_deps = [
        "//rule/dev_launch_wrapper",
        "@minecraft//:%s_client_libraries" % game_version,
        ":mod_unmapped",
        ":remapped_deps",
        "//blazerod:blazerod_without_jij",
        "//game:remapped_client_named",
        "//game:remapped_iris",
        "//game:remapped_sodium",
        "@maven//:com_h2database_h2",
        "@maven//:com_illposed_osc_javaosc_core",
        "@maven//:io_github_douira_glsl_transformer",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:net_fabricmc_yarn_v2",
        "@maven//:org_anarres_jcpp",
        "@maven//:org_antlr_antlr4_runtime",
    ],
)

java_binary(
    name = "server",
    srcs = [],
    data = [":remap_classpath"],
    jvm_flags = [
        "-Dfabric.development=true",
        "-Dfabric.remapClasspathFile=$(rlocationpath :remap_classpath)",
        "-Ddev.launch.version=%s" % game_version,
        "-Ddev.launch.type=server",
        "-Ddev.launch.mainClass=net.fabricmc.loader.impl.launch.knot.KnotServer",
        "-Ddev.launch.expandRunfileProperties=fabric.remapClasspathFile",
        "-Xmx4G",
    ],
    main_class = "top.fifthlight.fabazel.devlaunchwrapper.DevLaunchWrapper",
    runtime_deps = [
        "//rule/dev_launch_wrapper",
        "@minecraft//:%s_server_libraries" % game_version,
        ":mod_unmapped",
        ":remapped_deps",
        "//game:remapped_server_named",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:net_fabricmc_yarn_v2",
    ],
)


modrinth_dependency(
    name = "fabric_api",
    project_id = "P7dR8mSH",
    version_id = fabric_api_version_modrinth,
    dependency_type = "required",
)

modrinth_dependency(
    name = "fabric_language_kotlin",
    project_id = "Ha28R6CL",
    version_id = fabric_language_kotlin_version_modrinth,
    dependency_type = "required",
)

modrinth_dependency(
    name = "modmenu",
    project_id = "mOgUt4GM",
    dependency_type = "optional",
)

upload_modrinth(
    name = "upload_modrinth",
    token_secret_id = "modrinth_token",
    project_id = "tLPuDSOw",
    version_name = mod_version,
    version_id = mod_version,
    version_type = "alpha",
    changelog = "//:update_log",
    game_versions = [game_version],
    loaders = ["fabric"],
    deps = [
        ":fabric_api",
        ":fabric_language_kotlin",
        ":modmenu",
    ],
    file = ":mod",
    file_name = "ArmorStand-%s.jar" % mod_version,
)
